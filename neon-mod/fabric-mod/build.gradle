plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'fabric-loom' version '1.4-SNAPSHOT'
    id 'maven-publish'
}

loom {
    splitEnvironmentSourceSets()
    mods {
        "neon" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    shadow implementation(project(':neon-api'))
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

java {
    withSourcesJar()
}

sourcesJar {
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

jar {
    archiveClassifier = ''
}

shadowJar {
    from(sourceSets.client.output)
    archiveFileName = "${project.parent.name}-${project.version}-FABRIC.jar"
    archiveClassifier = ''
    configurations = [project.configurations.shadow]
}

remapJar {
    dependsOn(shadowJar)
    inputFile = tasks.shadowJar.archiveFile
}

tasks.assemble.dependsOn tasks.shadowJar
tasks.build.dependsOn tasks.shadowJar